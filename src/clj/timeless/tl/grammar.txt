<S> = operation-1


<exp> = group | application | section | set | seq | tuple | str-ws | name-ws | number-ws | quote | unquote


group = left-paren gte-0 right-paren
application = exp exp+
<section> = left-section | right-section | prefix-op
prefix-op = left-paren op right-paren



truncated-embedded = op-10 gt-10


(*
Sets can't have empty elements.
*)

clause-maybe-embedded = gt-1

<clause-exp> = truncated-embedded | clause-maybe-embedded
<clause-segment> = (guard-op gt-1) | (arrow-op clause-exp)

<abbreviated-clause> = clause-segment+
<unabbreviated-clause> = clause-exp clause-segment*

clause = abbreviated-clause | unabbreviated-clause
set = (set-left set-right) | (set-left (clause comma)* clause set-right)



(*
Seqs and tuple can have empty elements, as sections.

There is no way to make a seq or tuple section with a single empty (epsilon) element.
*)

element-maybe-embedded = gte-0

<element> = truncated-embedded | element-maybe-embedded
<element-maybe-empty> = element | empty-element
empty-element = epsilon

seq = seq-left
      (
      element? |
      ((element-maybe-empty comma)+ element-maybe-empty) |
      (element (comma element)? range-ws element?)
      )
      seq-right

tuple = left-paren (element-maybe-empty comma)+ element-maybe-empty right-paren



(*
string literal
*)
<str-ws> = ws str ws
str = #'\".*\"'



(*
number literal
*)
<number-ws> = ws number ws
number = #'\d+(?:\.\d+)?'



<name-ws> = ws name ws
name = declared-name | regular-name
<regular-name> = !op #'[a-zA-Z_]\w*'



<comma> = ws <','> ws

<ws> = (ws-only | comment)*
<ws-only> = <#'\s+'>
<comment> = <#'#.*\n'>

<set-left> = ws <'{'> ws
<set-right> = ws <'}'> ws

<seq-left> = ws <'['> ws
<seq-right> = ws <']'> ws

<left-paren> = ws <'('> ws
<right-paren> = ws <')'> ws

<range-ws> = ws range ws
range = '..'

<quote-op> = ws <"'"> ws
<unquote-op> = ws <"~"> ws



<quotable> = group | set | seq | tuple | str-ws | name-ws | number-ws | op
quote = quote-op quotable
unquote = unquote-op quotable



(*
The arrow operation is right associative.
*)

<left-0> = left-paren (truncated-embedded | gt-0) arrow-op right-paren
<right-0> = left-paren arrow-op (truncated-embedded | gte-0) right-paren
operation-0 = (truncated-embedded | gt-0) arrow-op (truncated-embedded | gte-0)

<arrow-op> = op-ws-0
<op-ws-0> = ws op-0 ws
op-0 = '->' | 'â†’'

<gte-0> = exp | _gte-0
<gt-0> = exp | _gt-0
<_gte-0> = operation-0 | _gt-0
<_gt-0> = _gte-1



(*
The guard operation is left associative.
*)

<left-1> = left-paren (truncated-embedded | gte-1) guard-op right-paren
<right-1> = left-paren guard-op gt-1 right-paren
operation-1 = (truncated-embedded | gte-1) guard-op gt-1

<guard-op> = op-ws-1
<op-ws-1> = ws op-1 ws
op-1 = '|'

<gte-1> = exp | _gte-1
<gt-1> = exp | _gt-1
<_gte-1> = operation-1 | _gt-1
